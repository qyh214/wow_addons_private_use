actions.precombat+=/apply_poison
# Swap to priority rotation if the variable is enabled
actions.precombat+=/variable,name=priority_rotation,value=toggle.funnel
actions.precombat+=/variable,name=trinket_sync_slot,value=1,if=trinket.1.has_stat.any_dps&(!trinket.2.has_stat.any_dps|trinket.1.is.treacherous_transmitter|trinket.1.cooldown.duration>=trinket.2.cooldown.duration)
actions.precombat+=/variable,name=trinket_sync_slot,value=2,if=trinket.2.has_stat.any_dps&(!trinket.1.has_stat.any_dps|trinket.2.cooldown.duration>trinket.1.cooldown.duration)
actions.precombat+=/stealth


actions+=/stealth
actions+=/kick
actions+=/variable,name=stealth,value=buff.shadow_dance.up|buff.stealth.up|buff.vanish.up
actions+=/variable,name=skip_rupture,value=buff.shadow_dance.up|!buff.slice_and_dice.up|buff.darkest_night.up|active_enemies>=8&!talent.replicating_shadows&talent.unseen_blade
actions+=/variable,name=maintenance,value=(dot.rupture.ticking|variable.skip_rupture)&buff.slice_and_dice.up
actions+=/variable,name=secret,value=buff.shadow_dance.up|(cooldown.flagellation.remains<40&cooldown.flagellation.remains>20&talent.death_perception)
actions+=/variable,name=racial_sync,value=(buff.shadow_blades.up&buff.shadow_dance.up)|!talent.shadow_blades&buff.symbols_of_death.up|boss&fight_remains<20
actions+=/variable,name=shd_cp,value=combo_points<=1|buff.darkest_night.up&combo_points>=7|effective_combo_points>=6&talent.unseen_blade
# Cooldowns
actions+=/call_action_list,name=cds
# Racials
actions+=/blood_fury,if=variable.racial_sync
actions+=/berserking,if=variable.racial_sync
actions+=/fireblood,if=variable.racial_sync&buff.shadow_dance.up
actions+=/ancestral_call,if=variable.racial_sync
## actions+=/invoke_external_buff,name=power_infusion,if=buff.shadow_dance.up
# Items (Trinkets)
actions+=/use_item,name=treacherous_transmitter,if=cooldown.flagellation.remains<=2|boss&fight_remains<=15
## actions+=/do_treacherous_transmitter_task,if=buff.shadow_dance.up|boss&fight_remains<=15
actions+=/use_item,name=imperfect_ascendancy_serum,use_off_gcd=1,if=dot.rupture.ticking&buff.flagellation_buff.up
actions+=/use_item,name=mad_queens_mandate,if=(!talent.lingering_darkness|buff.lingering_darkness.up|equipped.treacherous_transmitter)&(!equipped.treacherous_transmitter|trinket.treacherous_transmitter.cooldown.remains>20)|boss&fight_remains<=15
actions+=/use_items,slots=trinket1,if=(variable.trinket_sync_slot=1&(buff.shadow_blades.up|boss&fight_remains<=20)|(variable.trinket_sync_slot=2&(!trinket.2.cooldown.ready&!buff.shadow_blades.up&cooldown.shadow_blades.remains>20))|!variable.trinket_sync_slot)
actions+=/use_items,slots=trinket2,if=(variable.trinket_sync_slot=2&(buff.shadow_blades.up|boss&fight_remains<=20)|(variable.trinket_sync_slot=1&(!trinket.1.cooldown.ready&!buff.shadow_blades.up&cooldown.shadow_blades.remains>20))|!variable.trinket_sync_slot)

# Cooldowns for Stealth
actions+=/call_action_list,name=stealth_cds,if=!variable.stealth
# Finishing Rules
actions+=/call_action_list,name=finish,if=!buff.darkest_night.up&effective_combo_points>=6|buff.darkest_night.up&combo_points=cp_max_spend
# Combo Point Builder
actions+=/call_action_list,name=build
# Filler, Spells used if you can use nothing else.
actions+=/call_action_list,name=fill,if=!variable.stealth

# Combo Point Builder
actions.build+=/shadowstrike,cycle_targets=1,if=debuff.find_weakness.remains<=2&active_enemies=2&talent.unseen_blade|!used_for_danse&!talent.premeditation
actions.build+=/shuriken_tornado,if=buff.lingering_darkness.up|talent.deathstalkers_mark&cooldown.shadow_blades.remains>=32&active_enemies>=3|talent.unseen_blade&buff.symbols_of_death.up&active_enemies>=4
actions.build+=/shuriken_storm,if=buff.clear_the_witnesses.up&active_enemies>=2
actions.build+=/shadowstrike,cycle_targets=1,if=talent.deathstalkers_mark&!debuff.deathstalkers_mark.up&active_enemies>=3&(buff.shadow_blades.up|buff.premeditation.up|talent.the_rotten)
actions.build+=/shuriken_storm,if=talent.deathstalkers_mark&!buff.premeditation.up&active_enemies>=(2+3*buff.shadow_dance.up)|buff.clear_the_witnesses.up&!buff.symbols_of_death.up|buff.flawless_form.up&active_enemies>=3&!variable.stealth|talent.unseen_blade&buff.the_rotten.stack=1&active_enemies>=7&buff.shadow_dance.up
actions.build+=/shadowstrike
actions.build+=/goremaws_bite,if=combo_points.deficit>=3
actions.build+=/gloomblade
actions.build+=/backstab

# Cooldowns
actions.cds+=/cold_blood,if=cooldown.secret_technique.up&buff.shadow_dance.up&combo_points>=6&variable.secret&(!talent.flagellation|buff.flagellation_persist.up)
actions.cds+=/potion,if=buff.bloodlust.up|boss&fight_remains<30|buff.flagellation_buff.up
actions.cds+=/symbols_of_death,if=(buff.symbols_of_death.remains<=3&variable.maintenance&(!talent.flagellation|cooldown.flagellation.remains>=30-15*!talent.death_perception&cooldown.secret_technique.remains<8|!talent.death_perception)|boss&fight_remains<=15)
actions.cds+=/shadow_blades,if=variable.maintenance&variable.shd_cp&buff.shadow_dance.up&!buff.premeditation.up
actions.cds+=/thistle_tea,if=buff.shadow_dance.remains>2&!buff.thistle_tea.up
actions.cds+=/flagellation,if=combo_points>=5&cooldown.shadow_blades.remains<=3|boss&fight_remains<=25

# This list usually contains Cooldowns with neglectable impact that causes global cooldowns
actions.fill+=/arcane_torrent,if=energy.deficit>=15+energy.regen
actions.fill+=/arcane_pulse
actions.fill+=/lights_judgment
actions.fill+=/bag_of_tricks

# [Syrif] - This variable prevents rupture spam when CDs are toggled off, or otherwise desynced due to dungeon gameplay environment 
actions.finish+=/variable,name=cooldowns_soon,op=setif,condition=!toggle.cooldowns,value=false,value_else=cooldown.shadow_blades.remains<=13&cooldown.flagellation.remains<10&(dot.rupture.remains<(cooldown.shadow_blades.remains+buff.shadow_blades.duration))
actions.finish+=/secret_technique,if=variable.secret
# Maintenance Finisher
actions.finish+=/rupture,if=!variable.skip_rupture&(!dot.rupture.ticking|refreshable)&target.time_to_die-remains>6
actions.finish+=/rupture,cycle_targets=1,if=!variable.skip_rupture&!variable.priority_rotation&target.time_to_die>=(2*combo_points)&refreshable&active_enemies>=2
# [Syrif] Uses Hekili-specific variable as a workaround
actions.finish+=/rupture,if=talent.unseen_blade&variable.cooldowns_soon
# Direct Damage Finisher
actions.finish+=/coup_de_grace,if=debuff.fazed.up
actions.finish+=/black_powder,if=!variable.priority_rotation&variable.maintenance&((active_enemies>=2&talent.deathstalkers_mark&(!buff.darkest_night.up|buff.shadow_dance.up&active_enemies>=5))|talent.unseen_blade&active_enemies>=7)
actions.finish+=/eviscerate

# Shadow Dance, Vanish, Shadowmeld
actions.stealth_cds+=/shadow_dance,if=variable.shd_cp&variable.maintenance&cooldown.secret_technique.remains<=24&(buff.symbols_of_death.remains>=6|buff.shadow_blades.remains>=6)|boss&fight_remains<=10
actions.stealth_cds+=/vanish,if=energy>=40&!buff.subterfuge.up&effective_combo_points<=3
actions.stealth_cds+=/shadowmeld,if=energy>=40&combo_points.deficit>=3